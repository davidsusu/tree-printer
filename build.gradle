plugins {
    id 'java-library'
    id 'signing'
    id 'maven'
    id 'eclipse'
    id 'com.palantir.git-version' version '0.12.3'
}

sourceCompatibility = "1.8";
targetCompatibility = "1.8";

group = 'hu.webarticum'
version = gitVersion().replaceAll(/^v|\.dirty$/, '')
description = 'Simple Java library for visualizing tree structures in the command line'

def moduleName = 'hu.webarticum.treeprinter'
def scmUrl = 'https://github.com/davidsusu/tree-printer.git'
def websiteUrl = scmUrl.replaceAll(/\.git$/, "");

repositories {
    jcenter()
}

configurations {
    demo
}

sourceSets {
    demo {
        java {
            srcDirs = ['src/demo/java']
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

signing {
    required { true }
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

eclipse {
    classpath {
        plusConfigurations.add configurations.demo
    }
}

jar {
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

if (project.ext.properties.containsKey('mavenRepositoryUrl')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: project.ext.mavenRepositoryUrl) {
                    authentication(
                        userName: project.ext.mavenRepositoryUsername,
                        password: project.ext.mavenRepositoryPassword
                    )
                }
                beforeDeployment { MavenDeployment deployment ->
                    signing.signPom(deployment)
                }
                pom.artifactId = project.name
                pom.project {
                    name = project.name
                    description = project.description
                    url = websiteUrl
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    scm {
                        connection 'scm:git:' + scmUrl
                        developerConnection 'scm:git:' + scmUrl
                        url scmUrl
                    }
                    developers {
                        developer {
                            id = 'davidsusu'
                            name = 'Dávid Horváth'
                            email = 'horvath@webarticum.hu'
                        }
                    }
                }
            }
        }
    }
}
